plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

group 'com.telesign'
version '2.2.4'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:[3.3,4.0)'
    compile 'com.google.code.gson:gson:[2.7,3.0)'

    testCompile group: 'junit', name: 'junit', version: '[4.1,)'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '[3.7.0,4.0)'
}

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
    }
}

if (System.getenv('TRAVIS') == null) {
    apply plugin: 'maven'
    apply plugin: 'signing'

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task fatJar(type: Jar) {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }

    task clearOldJar(type: Delete) {
        delete "bin"
    }

    task copyJar(type: Copy) {
      dependsOn clearOldJar
      from fatJar
      into('bin')
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project.findProperty('ossrhUsername'), password: project.findProperty('ossrhPassword'))
                }

                pom.project {
                    name 'TeleSign SDK'
                    packaging 'jar'
                    description 'The TeleSign Java SDK is a Java library that provides an interface to TeleSign Web ' +
                            'Services. TeleSign Web Services conform to the REST Web Service Design Model. Services are ' +
                            'exposed as URI-addressable resources through the set of RESTful procedures in our TeleSign ' +
                            'REST API.'
                    url 'https://github.com/TeleSign/java_telesign'

                    scm {
                        connection 'scm:git:https://github.com/TeleSign/java_telesign.git'
                        developerConnection 'scm:git:git@github.com:TeleSign/java_telesign.git'
                        url 'https://github.com/TeleSign/java_telesign'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                        license {
                            name 'Apache Software License (ASL) version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name 'TeleSign Corp.'
                            organizationUrl 'https://www.telesign.com'
                        }
                    }
                }
            }
        }
    }
}
